plugins {
    id 'com.android.application'
}

android {
    compileSdk 36
    defaultConfig {
        applicationId "com.thomaskuenneth.mintime"
        minSdk 26
        targetSdk 36
        versionName "1.8.4"
        versionCode 56
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            // F-Droid: Reproducible Builds
            // Disables VCS info which can change between builds
            vcsInfo {
                include false
            }
        }
        debug {
            minifyEnabled false
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    namespace 'com.thomaskuenneth.mintime'

    // F-Droid: Reproducible Builds
    // Disables a proprietary Google blob that changes every build
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

// F-Droid: Reproducible Builds
// https://f-droid.org/docs/Reproducible_Builds/#publishing-apks-with-the-upstream-developers-signature
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation "com.google.android.material:material:1.13.0"
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "androidx.preference:preference:1.2.1"

    implementation "androidx.window:window-core:1.5.1"
    implementation "androidx.window:window:1.5.1"
    implementation "androidx.window:window-java:1.5.1"

    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
    implementation 'androidx.core:core-ktx:1.17.0'
}
